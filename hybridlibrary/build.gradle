if(IS_BUILD_MODULE.toBoolean()){
    apply plugin: 'com.android.library'
}else{
    apply plugin: 'com.android.application'
}
android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion
    defaultConfig {
        if(!IS_BUILD_MODULE.toBoolean()){
            applicationId "com.sheyuan.hybridlibrary"
        }
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk{
            abiFilters "armeabi","armeabi-v7a","x86","mips"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //保证libs文件夹下面只有一个armeabi的so文件夹不然在64位cpu手机上会出现问题
    sourceSets{
        main{
            if (IS_BUILD_MODULE.toBoolean()) {
                manifest.srcFile 'src/main/buildModule/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/buildApp/AndroidManifest.xml'
            }
            jniLibs.srcDirs = ['libs']
        }
    }
    //避免library之间资源重名,所有资源均用module名字开头
    resourcePrefix 'hybrid_'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile project(':baselibrary')
}
